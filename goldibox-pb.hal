#!/usr/bin/env halrun
# #######################################
#
# HAL file for Goldibox project
#
# http://linuxcnc.org/docs/devel/html/man/man1/halcmd.1.html

# ################################################
# Setup
# ################################################

# ################
# Load comps

# Launch the setup script to make sure hardware setup looks good
loadusr -w ./setup.sh

# Thermistor ADC values
# 100k thermistor on AIN5
# https://github.com/machinekit/machinekit/blob/master/src/hal/user_comps/hal_temp_bbb.py
loadusr -Wn Therm hal_temp_bbb -n Therm -c 00:TR91,01:TR91 -b CRAMPS -r 1000

# GPIO drivers
# - hal_bb_gpio docs
# http://linuxcnc.org/docs/devel/html/man/man9/hal_bb_gpio.9.html
# - pinouts
# http://beagleboard.org/support/bone101
loadrt hal_bb_gpio board=PocketBeagle output_pins=102,104,106,120

# Incubater userland comp
loadusr -Wn goldibox ./goldibox.py

# Logic for on/off switch and heat/cool selector
loadrt lut5 names=peltier-pos,peltier-neg

# ################
# Threads

loadrt threads name1=servo-thread period1=10000000
addf bb_gpio.read                   servo-thread
addf peltier-pos                    servo-thread
addf peltier-neg                    servo-thread
addf bb_gpio.write                  servo-thread

# ################
# Goldibox comp

# Inputs
# - Min/max/current temp settings
net temp-min                        => goldibox.temp-min
net temp-max                        => goldibox.temp-max
net temp-cur                        => goldibox.temp-cur
# - Hysteresis
net hysteresis                      => goldibox.hysteresis
# - Enable
net enable                          => goldibox.enable
# - Shut down
net shutdown                        => goldibox.shutdown
sets shutdown 0

# Outputs
# - On/off switch
net switch-on                       <= goldibox.switch-on
# - Heat/cool selector switch (1/0)
net switch-heat                     <= goldibox.switch-heat
# - Error
net error                           <= goldibox.error


# ################
# Temp in signal

net temp-cur                        <= Therm.ch-05.value


# ################
# Peltier out signals

# Enable
net enable                          => bb_gpio.p1.out-06

# Peltier +/- LUT
# http://linuxcnc.org/docs/devel/html/man/man9/lut5.9.html
#
# on/1	heat/0	| +	-	| + val	| - val
# 0	0	| 0	0	|	|
# 0	1	| 0	0	|	|
# 1	0	| 1	0	|   0x4	|
# 1	1	| 0	1	|	|   0x8
#                                 ----- | -----
setp peltier-pos.function	    0x4
setp peltier-neg.function		    0x8

# Peltier pos. LUT
# - Inputs
net switch-on                       => peltier-pos.in-1
net switch-heat                     => peltier-pos.in-0
# - Output to GPIO
net peltier-pos                     <= peltier-pos.out
net peltier-pos                     => bb_gpio.p1.out-02

# Peltier neg. LUT
# - Inputs
net switch-on                       => peltier-neg.in-1
net switch-heat                     => peltier-neg.in-0
# - Output to GPIO
net peltier-neg                     <= peltier-neg.out
net peltier-neg                     => bb_gpio.p1.out-04


# ################
# Fan out signals

# Output
# - Switch on output to GPIO
net switch-on                       => bb_gpio.p1.out-20


# ################
# Enable

sets enable 1


# ################################################
# Run
# ################################################

start
waitusr goldibox


# ################################################
# Shut down
# ################################################

# Stop threads
stop

# Disable peltier
unlinkp bb_gpio.p1.out-06
setp bb_gpio.p1.out-06 0

# Turn off fan
unlinkp bb_gpio.p1.out-20
setp bb_gpio.p1.out-20 0

# Unload comps
unload all
